! ------------------------------------------------------------------------------

! Before this GO script is called, the
! VARIABLE "factor" must already be defined.

! test IF VARIABLE exists
LET/D ($1) = 0 ! default = dummy scalar value
LET datasize = `$1,RETURN=SIZE`
IF `datasize EQ 1` THEN
  IF ($undefinedinfo) THEN SAY ($1) is undefined
  EXIT
ENDIF

! this ugly test is necessary in case the unit is empty
DEFINE SYMBOL emptytest = prefix`$1, RETURN=unit`postfix
IF ($unit%TRUE|fromdataset>TRUE|*>FALSE|%) THEN
  IF ($emptytest%|prefix postfix>TRUE|*>FALSE|%) THEN
    DEFINE SYMBOL plotunit = 1 ! 1 = dimensionless
  ELSE
    DEFINE SYMBOL plotunit = `$1, RETURN=unit`
  ENDIF
ELSE
  DEFINE SYMBOL plotunit = ($unit)
ENDIF

GO nextviewport

LET alldata  = factor*$1
LET plotdata = alldata[i=($plotlon),j=($plotlat),k=($startlevel):($endlevel),t="($plotday)"]

! ------------------------------------------------------------------------------

! DEFINE range of PLOT DATA
LET minval = plotdata[k=@MIN]
LET maxval = plotdata[k=@MAX]
IF ($rangeinfo) THEN SAY $1 ranges from `minval` to `maxval`

! calculate lg of PLOT range
IF `(maxval EQ 0) AND (minval EQ 0)` THEN
  LET lgrange = 0
ELSE
  LET lgrange = INT(LOG(maxval-minval))-1
  IF `(maxval LT 0)` THEN LET lgrange = INT(LOG((-1)*minval))-1
  IF `(minval GT 0)` THEN LET lgrange = INT(LOG(maxval))-1
ENDIF

! apply lgrange to PLOT and ytitle
IF `(lgrange GT 2) OR (lgrange LT -2)` THEN
  DEFINE SYMBOL scaledplotunit = *1E`lgrange` ($plotunit)
  LET scalefactor = 1/10^lgrange
ELSE
  DEFINE SYMBOL scaledplotunit = ($plotunit)
  LET scalefactor = 1
ENDIF

IF ($plotunit%|mol/mol>TRUE|mol/mol/day>TRUE|*>FALSE|%) THEN
  ! scale to pmol/mol OR nmol/mol OR micromol/mol
  IF `(lgrange GE -12) AND (lgrange LT -9)` THEN
    DEFINE SYMBOL scaledplotunit = p($plotunit)
    LET scalefactor = 1e12
  ENDIF
  IF `(lgrange GE -9) AND (lgrange LT -6)` THEN
    DEFINE SYMBOL scaledplotunit = n($plotunit)
    LET scalefactor = 1e9
  ENDIF
  IF `(lgrange GE -6) AND (lgrange LT -3)` THEN
    DEFINE SYMBOL scaledplotunit = @sgm@as($plotunit)
    LET scalefactor = 1e6
  ENDIF
ENDIF

! ------------------------------------------------------------------------------

! DEFINE the PLOT TITLE
IF ($2%FALSE|*>TRUE%)  THEN
  ! USE second parameter as PLOT TITLE
  DEFINE SYMBOL plottitle = $2
ELSE
  ! calculate PLOT TITLE from $1
  IF ($($1)%FALSE|*>TRUE%) THEN
    DEFINE SYMBOL plottitle = ($($1))
  ELSE
    DEFINE SYMBOL plottitle = `$1, RETURN=TITLE`
  ENDIF
  IF ($plotunit%|mol/l>TRUE|*>FALSE|%) THEN
    ! add square brackets (math font 22 and 23) to denote concentrations
    DEFINE SYMBOL plottitle = @ma22@sr($plottitle)@ma23@sr
  ENDIF
ENDIF

! ------------------------------------------------------------------------------

LET/UNITS="@as($scaledplotunit)" scaledplotdata = scalefactor * plotdata

PLOT/SET/TITLE="@sr($plottitle)" scaledplotdata

GO ppl_mylayout
PPL PLOT

! ------------------------------------------------------------------------------
