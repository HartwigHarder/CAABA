#! /bin/tcsh -f
# Time-stamp: <2010-06-14 14:36:59 sander>
# create a self-consistent zip file of a new caaba version

if ( "$1" == "" ) then
  echo ""
  echo "Info (before making a new version):"
  set current_version = `grep -i "modver *=" messy_mecca.f90 | sed "s|.*'\(.*\)'.*|\1|g"`
  echo "- update version number (modver = '$current_version'):"
  echo "  e messy_mecca.f90"
  echo "- LaTeX:"
  echo "  switch on citetags in xmecca"
  echo "  cd mecca ; xmecca latex"
  echo "  switch citetags in xmecca off again"
  echo "- Enter standard settings:"
  echo "  Makefile:        COMPILER = g95"
  #echo "                   OUTPUT = ASCII or NETCDF?"
  echo "                   F90FLAGS = fast or with checks?"
  #echo "  mecca.nml:       mecca_aero = 'AUTO'"
  echo '  setmodelrun.jnl: DEFINE SYMBOL C2 = `{spawn: "cd .. ; pwd"}`'
  echo "                   GO _define_sensi ... latest run (no other plots)"
  echo "  run xcaaba simple"
  echo "    (and choose caaba_simple.nml)"
  echo "- add all changes and current date to CHANGELOG"
  echo "Usage:"
  echo "  ncv nn"
  echo "  (nn = version number)"
  echo "Examples:"
  echo "  snapshot with current date:        ncv `date +'%Y%m%d'`"
  echo "  proper version number, e.g.:       ncv $current_version"
  echo ""
  exit
endif

if (! -e mecca/smcl/messy_mecca_kpp_parameters.f90) then
  echo "\nPlease run xmecca with KPP (not KP4) before executing ncv\!"
  exit 1
endif

if (! -e sfmakedepend) then
  echo "\nThe perl script sfmakedepend does not exist. Please run"
  echo "configure to create it from sfmakedepend.pl.in:"
  echo "  ( cd ../../.. ; ./configure ; cd - )\n"
  exit 1
endif

set basedir = `basename $PWD`
set dirname = ${basedir}_$1
set oridir  = $HOME/messy/mecca/ori

if ( -d $oridir) then
  set zipfile = $oridir/$dirname.zip
else
  echo "Error: Directory $oridir/ does not exist."
  exit 1
endif

if ( -e $zipfile) then
  echo "\nError, the zip file exists already:"
  ls -la $zipfile
  echo "Please choose another version number\!"
  exit 1
endif

set MECCA_VERSION = `grep -i "modver *=" messy_mecca.f90 | sed "s|.*'\(.*\)'.*|\1|g"`
set infofile = "manual/mecca_info.tex"
set dontedit = "this file was created automatically by ncv, do not edit"
echo "% $dontedit"                          > $infofile
echo "\\def\\meccaversion{$MECCA_VERSION}" >> $infofile
echo "Update MECCA Manual? [y/n/q, default=n]"
set inputstring = $<
if ( $inputstring == 'q' ) exit
if ( $inputstring == 'y' ) then
  cd manual
  pdflatex caaba_mecca_manual.tex
  bibtex caaba_mecca_manual.aux
  pdflatex caaba_mecca_manual.tex
  pdflatex caaba_mecca_manual.tex
  cd -
endif

# temporarily rename the caaba directory to include the version:
cd .. ; mv $basedir $dirname ; cd $dirname

# create zip file:
_zipcaaba.tcsh zip

# rename the caaba directory back to its original name:
cd .. ; mv $dirname $basedir ; cd $basedir

# move zip file to final directory:
mv $dirname.zip $zipfile

echo "\nThe zipfile has been created:"
ls -la $zipfile

exit
